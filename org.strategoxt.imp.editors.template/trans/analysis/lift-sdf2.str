module analysis/lift-sdf2

imports 
  
  include/TemplateLang
  analysis/attributes
  
strategies
  
  normalize-all = bottomup(try(normalize))
  
rules
  
  normalize: no-attrs() -> attrs([])
  
  normalize:
    TemplateProduction(s, Constr(c), r, attrs(a*)) -> TemplateProduction(s, Constr(c), r, attrs(a'*))
    with
      c'  := <double-quote> c
    ; a'* := [sdf-cons(c'), a*]

  normalize:
    SdfProduction(s, Constr(c), r, attrs(a*)) -> SdfProduction(s, Constr(c), r, attrs(a'*))
    with
      c'  := <double-quote> c
    ; a'* := [sdf-cons(c'), a*]
      
  normalize: 
  	prod(sym*, sort(s), attrs(a*)) -> SdfProduction(s, c, Rhs(sym*), attrs(a*))
    with
        c := <getfirst(lift-constr)> a*
     <+ c := Inj() 
    
  lift-constr:
    sdf-cons(x) -> Constr(<un-double-quote> x)
    
  lift-constr:
    Constructor(x) -> Constr(<un-double-quote> x)
  
