module generation-utils
imports
  libstratego-lib
  lib/editor-common.generated
  include/Template
  analysis

signature
  constructors
    Newlines : X
    ImportPath : X

strategies

  add-anno(|anno):
    x{a*} -> x{anno, a*}

  has-anno(|anno):
    x{a*} -> <id>
    where <one(?anno)> a*

  trim-leading-layout =
    repeat(  \[Layout(_)  | tail] -> tail\
           + \[Newline(_) | tail] -> tail\ )

  trim-trailing-layout =
    listbu(   \[Layout(_)] -> []\
           <+ \[Newline(_)] -> []\ <+ id)

  trim-layout =
    trim-leading-layout;
    trim-trailing-layout

  // origin-column (obviously) doesn't work in an imported file...
  // get-indent-string =
  //   filter(not(?Layout(_) + ?Newline(_)); origin-column); list-min => size;
  //   // Build string from right to left, as list.
  //   ![]; repeat(![' ' | <id>] | size); !['\n' | <id>];
  //   implode-string

  // This doesn't take the indentation due to everything up to
  // the open brackets of the template into account.
  get-indent-string =
    let get-indent =
             \[Newline(_), Layout(y), _ | tail] -> [<string-length> y | <get-indent> tail]\
          <+ \[Newline(_), Layout(_)] -> []\
          <+ \[Newline(_), Newline(_)] -> []\
          <+ \[Newline(_), y | tail] -> [0 | <get-indent> tail]\
          <+ \[_ | tail] -> <get-indent> tail\
          <+ []
    in
      get-indent; list-min => size;
      // Build string from right to left, as list.
      ![]; repeat(![' ' | <id>] | size); !['\n' | <id>];
      implode-string
    end

  re-indent(|indent-string) =
    \[Newline(x), Layout(y) | tail] -> [Layout(z) | tail']
    with
      z := <string-replace(|indent-string, "\n")> <conc-strings>(x, y);
      tail' := <re-indent(|indent-string)> tail\
  <+
    \[head | tail] -> [head | <re-indent(|indent-string)> tail]\
  <+
    []

  try-re-indent =
    if get-indent-string => is then
      re-indent(|is)
    end

  use-no-newlines       = <Options> Newlines() => None()
  use-leading-newlines  = <Options> Newlines() => Leading()
  use-trailing-newlines = <Options> Newlines() => Trailing()

  newlines-switch(none, leading, trailing) =
    switch <Options> Newlines()
      case ?None(): none
      case ?Leading(): leading
      case ?Trailing(): trailing
    end

  normpath =
    ?original;
    string-tokenize(|['/', '\']);
    filter(not("."));
    repeat(   rec x( \[_, ".." | t] -> t\ <+ [id | x] ) // remove one "dir/.." pair
           <+ \[".." | t] -> t\ );                      // remove leading ".."
    separate-by(|"/");
    if <is-abspath> original then  // FIXME: is-abspath may be POSIX-only?
      !["/" | <id>]
    end;
    concat-strings
