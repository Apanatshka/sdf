module TemplateTest

imports
  Common
  Layout

options
  // When changing to leading or trailing, remove one "\n" separator below.
  newlines : none // none, leading or trailing

  // Absolute paths are relative to the project directory.
  // Relative paths are relative to the directory of the current file.
  str output : /trans/prettyprint.generated.str
  esv output : /editor/TemplateTest-Completions.generated2.esv
  sdf output : /syntax/TemplateTest.generated.def

context-free start-symbols
  Start

templates

  Start.Module = <<
    module <modulename:ID>

    <Definition*; separator="\n\n">
  >>

  Definition.Entity = <<
    entity <ID> {
      <Property*
        ; separator="\n" // (no newlines)
        // ; separator=""   // (leading/trailing newlines)
        >
    }
  >>

  Property.Property = [[
    [x:ID] : [T:Type]
  ]]

  Type.Type = <<<Type:ID>>>

  Definition.Expression = <<
    expression <ID> {
      <Expression>
    }
  >>

  Expression.Plus  = <<<Expression> + <Expression>>> {left}
  Expression.Minus = <<<Expression> - <Expression>>> {left}
  Expression.Times = <<<Expression> * <Expression>>> {left}
  Expression.Over  = <<<Expression> / <Expression>>> {left}
  Expression.Value = <<<INT>>>
  Expression = <<(<Expression>)>> {bracket}

context-free priorities

  {left: Expression.Times Expression.Over } >
  {left: Expression.Plus  Expression.Minus}
